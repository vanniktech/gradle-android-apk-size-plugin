buildscript {
  ext.kotlinVersion = '1.2.21'

  repositories {
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    classpath "com.gradle.publish:plugin-publish-plugin:0.9.9"
    classpath "com.github.ben-manes:gradle-versions-plugin:0.17.0"
    classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.11.0'
    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.9.0'
  }
}

apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'kotlin'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'com.vanniktech.code.quality.tools'
apply plugin: 'com.vanniktech.android.junit.jacoco'

codeQualityTools {
  ktlint {
    toolVersion = '0.15.0'
  }
  detekt {
    toolVersion = '1.0.0.RC6-2'
  }
  findbugs {
    enabled = false
  }
  pmd {
    enabled = false
  }
  checkstyle {
    enabled = false
  }
  cpd {
    enabled = false
  }
  errorProne {
    enabled = false
  }
}

repositories {
  google()
  gradlePluginPortal()
}

configurations.all {
  resolutionStrategy {
    force "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    force "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
  }
}

dependencies {
  implementation localGroovy()
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
  implementation "com.android.tools.build:gradle:3.0.1"

  testImplementation "org.spockframework:spock-core:1.1-groovy-2.4", {
    exclude module: 'groovy-all'
  }
}

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

group = GROUP
version = VERSION_NAME

pluginBundle {
  website = POM_URL
  vcsUrl = POM_SCM_URL

  plugins {
    androidApkSize {
      id = 'com.vanniktech.android.apk.size'
      displayName = 'Gradle Android APK Size Plugin'
      tags = ['gradle', 'android', 'apk', 'app module', 'size']
      description = POM_DESCRIPTION
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.5'
  distributionType = Wrapper.DistributionType.ALL
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
  classifier = 'groovydoc'
  from groovydoc.destinationDir
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives jar
  archives groovydocJar
  archives javadocJar
  archives sourcesJar
}

apply from: file('gradle/gradle-mvn-push.gradle')

tasks.withType(JavaCompile) {
  sourceCompatibility = JavaVersion.VERSION_1_7
  targetCompatibility = JavaVersion.VERSION_1_7

  configure(options) {
    compilerArgs << "-Xlint:all"            // Turn on all warnings
    compilerArgs << "-Werror"               // Turn warnings into errors
    encoding = "utf-8"
    incremental = true
    fork = true
  }
}

tasks.withType(GroovyCompile) {
  sourceCompatibility = JavaVersion.VERSION_1_7
  targetCompatibility = JavaVersion.VERSION_1_7

  configure(options) {
    compilerArgs << "-Xlint:all"            // Turn on all warnings
    compilerArgs << "-Werror"               // Turn warnings into errors
    encoding = "utf-8"
    incremental = true
    fork = true
  }

  configure(groovyOptions) {
    encoding = "utf-8"
    fork = true
  }
}

tasks.withType(Test) {
  testLogging {
    exceptionFormat "full"
    showCauses true
    showExceptions true
    showStackTraces true
    events = ["failed", "skipped"]
  }

  maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}
