buildscript {
  ext.kotlinVersion = '1.2.41'

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    classpath 'com.gradle.publish:plugin-publish-plugin:0.10.0'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.10.0'
    classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.11.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.2.0'
  }
}

apply plugin: 'groovy'
apply plugin: 'java-library'
apply plugin: 'java-gradle-plugin'
apply plugin: 'kotlin'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.vanniktech.code.quality.tools'
apply plugin: 'com.vanniktech.android.junit.jacoco'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: "com.vanniktech.maven.publish"

codeQualityTools {
  ktlint {
    toolVersion = '0.24.0'
  }
  detekt {
    toolVersion = '1.0.0.RC7'
  }
  findbugs {
    enabled = false
  }
  pmd {
    enabled = false
  }
  checkstyle {
    enabled = false
  }
  cpd {
    enabled = false
  }
  errorProne {
    enabled = false
  }
}

gradlePlugin {
  plugins {
    androidApkSizePlugin {
      id = 'com.vanniktech.android.apk.size'
      implementationClass = 'com.vanniktech.android.apk.size.ApkSizePlugin'
    }
  }
}

repositories {
  mavenCentral()
  google()
  jcenter()
}

dependencies {
  api gradleApi()
  api localGroovy()
  api "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

  compileOnly "com.android.tools.build:gradle:3.1.2"

  testImplementation "com.android.tools.build:gradle:3.1.2"
  testImplementation "org.spockframework:spock-core:1.1-groovy-2.4", {
    exclude module: 'groovy-all'
  }
}

sourceCompatibility = JavaVersion.VERSION_1_7

pluginBundle {
  website = POM_URL
  vcsUrl = POM_SCM_URL

  plugins {
    androidApkSizePlugin {
      displayName = 'Gradle Android APK Size Plugin'
      tags = ['gradle', 'android', 'apk', 'app module', 'size']
      description = POM_DESCRIPTION
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.8'
  distributionType = Wrapper.DistributionType.ALL
}
