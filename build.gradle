buildscript {
    apply from: rootProject.file("gradle/dependencies.gradle")

    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath plugs.kotlinGradlePlugin
        classpath plugs.pluginPublishPlugin
        classpath plugs.gradleVersionsPlugin
        classpath plugs.gradleAndroidJunitJacocoPlugin
    }
}

apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'kotlin'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'com.vanniktech.android.junit.jacoco'

repositories {
    maven { url "https://plugins.gradle.org/m2/" }
}

dependencies {
    implementation localGroovy()
    implementation deps.kotlinStdlib
    implementation plugs.gradle

    testImplementation deps.spockCore, { exclude module: 'groovy-all' }    // Use localGroovy()
}

sourceCompatibility = rootProject.ext.sourceCompatibilityVersion

group = GROUP
version = VERSION_NAME

pluginBundle {
    website = POM_URL
    vcsUrl = POM_SCM_URL

    plugins {
        androidApkSize {
            id = 'com.vanniktech.android.apk.size'
            displayName = 'Gradle Android APK Size Plugin'
            tags = ['gradle', 'android', 'apk', 'app module', 'size']
            description = POM_DESCRIPTION
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0.1'
    distributionType = Wrapper.DistributionType.ALL
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    from groovydoc.destinationDir
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives groovydocJar
    archives javadocJar
    archives sourcesJar
}

apply from: file('gradle/gradle-mvn-push.gradle')
